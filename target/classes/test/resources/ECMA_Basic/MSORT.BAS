10 REM BOTTOM-UP ITERATIVE MERGESORT
20 OPTION BASE 0
40 REM N IS NUMBER OF ELEMENTS USED IN ARRAY A
50 REM ARRAY A HAS THE DATA TO SORT
90 RANDOMIZE
100 LET N=1000
110 DIM A(1000),T(1000)
120 FOR I=0 TO N-1 STEP 1
130 LET A(I)=INT(RND*100000)
140 NEXT I
150 PRINT "UNSORTED LIST"
160 GOSUB 400
170 REM DO THE SORT
200 GOSUB 1000
210 PRINT "SORTED LIST"
220 GOSUB 500
230 STOP
400 REM SUBROUTINE TO PRINT ARRAY A CONTENTS
410 FOR I=0 TO N-1 STEP 1
420 PRINT "A(";I;")=";A(I)
430 NEXT I
440 RETURN
500 REM SUBROUTINE TO PRINT ARRAY A CONTENTS
510 REM AND CHECK THEY ARE SORTED
520 FOR I=0 TO N-2 STEP 1
530 IF A(I)<=A(I+1) THEN 550
540 PRINT "SORT ERROR AT THIS SLOT ";
550 PRINT "A(";I;")=";A(I)
560 NEXT I
570 PRINT "A(";N-1;")=";A(N-1)
580 RETURN
1000 REM BOTTOM-UP MERGESORT
1010 REM ARRAY A IS DATA TO SORT, ARRAY T IS TEMP ARRAY
1020 REM N IS NUMBER OF ELEMENTS
1030 LET W=1
1040 IF W>=N THEN 2060
1050 FOR I=0 TO N-1 STEP 2*W
1060 REM BOTTOMUPMEERGE(A,I,MIN(I+W,N),MIN(I+2*W,N),T)
1070 LET L=I
1080 LET R=I+W
1090 IF R<=N THEN 1110
1100 LET R=N
1110 LET E=I+2*W
1120 IF E<=N THEN 1140
1130 LET E=N
1140 GOSUB 5000
2000 NEXT I
2010 FOR I=0 TO N-1 STEP 1
2020 LET A(I)=T(I)
2030 NEXT I
2040 LET W=2*W
2050 GOTO 1040
2060 RETURN
5000 REM MERGE TWO RUNS
5010 REM MERGE A(L..R-1) AND A(R..E-1) TO T(L..E-1)
5020 LET I0=L
5030 LET I1=R
5040 FOR J=L TO E-1 STEP 1
5050 IF I0>=R THEN 5120
5060 IF I1>=E THEN 5090
5070 IF A(I0)<=A(I1) THEN 5090
5080 GOTO 5120
5090 LET T(J)=A(I0)
5100 LET I0=I0+1
5110 GOTO 5140
5120 LET T(J)=A(I1)
5130 LET I1=I1+1
5140 NEXT J
5150 RETURN
9999 END
