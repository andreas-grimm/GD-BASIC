10 REM MUTIPLY TWO 3 X 3 MATRICES AND SHOW RESULT
20 OPTION BASE 1
30 DIM A(3,3),B(3,3),C(3,3)
40 REM LOAD DATA FOR A
50 DATA 10,20,10,4,5,6,2,3,5
60 PRINT "MATRIX A:"
70 FOR I=1 TO 3
80 FOR J=1 TO 3
90 READ A(I,J)
100 PRINT A(I,J),
110 NEXT J
120 PRINT
130 NEXT I
140 REM LOAD DATA FOR B
150 DATA 3,2,4,3,3,9,4,4,2
160 PRINT "MATRIX B:"
170 FOR I=1 TO 3
180 FOR J=1 TO 3
190 READ B(I,J)
200 PRINT B(I,J),
210 NEXT J
220 PRINT
230 NEXT I
240 REM MULTIPLY, C = A X B
250 FOR I=1 TO 3
260 FOR J=1 TO 3
270 LET S=0
280 FOR K=1 TO 3
290 LET S=S+A(I,K)*B(K,J)
300 NEXT K
310 LET C(I,J)=S
320 NEXT J
330 NEXT I
340 PRINT "MATRIX C, PRODUCT OF MATRICES A AND B:"
350 FOR I=1 TO 3
360 FOR J=1 TO 3
370 PRINT C(I,J),
380 NEXT J
390 PRINT
400 NEXT I
410 END
