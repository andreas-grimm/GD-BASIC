100 'SANTA PARAVIA AND FIUMACCIO * COPYRIGHT REV. GEORGE BLANK
110 'ADAPTED FOR TRSDOS 2.2 07/01/79 * BILLY YAMASAKI
120 CLEAR700
130 DEFINTA-J,M-Q,T,V,W
140 Y(0)=1400
150 DATA"SIR ","BARON ","COUNT ","MARQUIS ","DUKE ","GRAND DUKE ","PRINCE ","**  HIS ROYAL HIGHNESS, KING "
160 DATA"LADY ","BARONESS ","COUNTESS ","MARQUISE ","DUCHESS ","GRAND DUCHESS ","PRINCESS ","**  HIS ROYAL HIGHNESS, QUEEN "
170 DATA"SANTA PARAVIA","FIUMACCIO","TORRICELLA","MOLINETTO","FONTANILE","ROMAGNA"
180 CLS:PRINT:PRINT"SANTA PARAVIA AND FIUMACCIO"
190 PRINT:FORA=1TO16:READA$:NEXT
200 INPUT"HOW MANY PLAYERS (1 TO 6)";F
210 FORA=1TOF:READT$(A)
220 PRINT:PRINT"WHO IS THE RULER OF ";T$(A);
230 INPUTN$(A)
240 PRINT:N$(A)=N$(A)+" OF "+T$(A)
250 PRINT"IS ";N$(A);" A MAN OR WOMAN (M/W)?":PRINT
260 V(A)=0:A$=INKEY$:IFA$<>"M"ANDA$<>"W"THEN260  
270 IFA$="W"THENV(A)=8
280 G(A)=25:H(A)=10:I(A)=5:J(A)=2:O(A)=1420+RND(35)
290 K(A)=1000:L(A)=10000:R(A)=5000:T(A)=1:U(A)=1
300 N(A)=4:P(A)=25:Q(A)=5:M(A)=25:S(A)=2000
310 NEXT
320 FORA=1TOF
330 RESTORE
340 B=V(A)+T(A)
350 FORC=1 TO B
360 READT$(A)
370 NEXTC
380 NEXTA
390 PRINT"INSTRUCTIONS REQ'D (Y/N)?"
400 A$=INKEY$:IFA$<>"Y"ANDA$<>"N"THEN400  
410 IFA$="N"THEN430  
420 GOSUB3680 
430 PRINT:PRINT"1-APPRENTICE 2-JOURNEYMAN 3-MASTER 4-GRAND MASTER":PRINT
440 INPUT"ENTER YOUR SKILL LEVEL";U(0)
450 IFU(0)<1U(0)=1
460 IFU(0)>4U(0)=4
470 U(0)=U(0)+5
480 E=E+1
490 IFT(E)=-1E=E+1
500 IF(T(1)<1)AND(T(2)<1)AND(T(3)<1)AND(T(4)<1)AND(T(5)<1)AND(T(6)<1)THENGOTO3910 
510 IFE>FTHENE=0:Y(0)=Y(0)+1:GOTO480  
520 IFY(0)>O(E)THEN480  
530 IFY(0)=O(E)THEN700  
540 GOSUB900  
550 GOSUB1450 
560 GOSUB1920 
570 GOSUB1680 
580 GOSUB2390 
590 GOSUB2840 
600 GOSUB3190 
610 GOTO480  
620 CLS
630 PRINT:FORA=1TOF:PRINTT$(A);N$(A)
640 PRINT:PRINT
650 PRINT"NOBLES SOLDIERS CLERGY MERCHANTS SERFS    LAND    TREASURY"
660 PRINTN(A);TAB(7)P(A);TAB(16)Q(A);TAB(23)M(A);TAB(32)S(A);TAB(40)L(A);TAB(50)K(A)
670 NEXT
680 PRINT:PRINT:PRINT:INPUT"                                  (PRESS ENTER)";A$
690 RETURN
700 CLS:PRINT:PRINT"VERY SAD NEWS":PRINT
710 PRINTT$(E);N$(E);" HAS JUST DIED"
720 T(E)=-1:Y=RND(8)
730 IFY(0)>1450PRINT"OF OLD AGE AFTER A LONG REIGN":GOTO790  
740 IFY<4PRINT"OF PNEUMONIA AFTER A COLD WINTER IN A DRAFTY CASTLE"
750 IFY=5PRINT"IN A SMALLPOX EPIDEMIC (DIDN'T BELIEVE IN IMMUNIZATION)"
760 IFY=4PRINT"OF TYPHOID AFTER DRINKING CONTAMINATED WATER"
770 IFY=6PRINT"AFTER BEING ATTACKED BY ROBBERS WHILE TRAVELING"
780 IFY>6PRINT"OF FOOD POISONING"
790 PRINT
800 INPUT"(PRESS ENTER)";A$
810 GOSUB2390 
820 GOSUB620  
830 GOTO480  
840 I!=INT(I!):RETURN
850 C!=INT(C!):RETURN
860 S!=INT(S!):RETURN
870 K(E)=INT(K(E)):RETURN
880 Z=RND(A)*S(E)/100:Z%=Z:PRINTZ%;"SERFS BORN THIS YEAR":S(E)=S(E)+Z%:RETURN
890 Z=RND(A)*S(E)/100:Z%=Z:PRINTZ%;"SERFS DIE THIS YEAR":S(E)=S(E)-Z%:RETURN
900 W=(RND(5)+RND(6))/2
910 ONWGOTO920  ,940  ,960  ,980  ,1000 
920 W$="DROUGHT    FAMINE THREATENS"
930 GOTO1010 
940 W$="BAD WEATHER    POOR HARVEST"
950 GOTO1010 
960 W$="NORMAL WEATHER    AVERAGE HARVEST"
970 GOTO1010 
980 W$="GOOD WEATHER    FINE HARVEST"
990 GOTO1010 
1000 W$="EXCELLENT WEATHER   GREAT HARVEST"
1010 R=RND(50)
1020 R(E)=(R(E)*100-R(E)*R)/100
1030 X=L(E):Y=(S(E)-D(E)*100)*5:IFY<0Y=0
1040 IFY<XTHENX=Y
1050 Y=R(E)*2:IFY<XTHENX=Y
1060 Y=W-.5:H!=X*Y:R(E)=R(E)+H!
1070 D!=N(E)*100+C(E)*40+M(E)*30+P(E)*10+S(E)*5
1080 L=(3*W+RND(6)+RND(6)+10)/10
1090 IFH!<1Y=2:GOTO1110 
1100 Y=D!/H!:IFY>2Y=2
1110 IFY<8Y=.8
1120 L=L*Y:L=INT(L*10):L=L/10
1130 Z=6-W
1140 G=(Z*3+RND(5)+RND(5))/5*Y*20
1150 RETURN
1160 PRINT
1170 PRINT"RATS ATE";R;" % OF YOUR GRAIN RESERVES"
1180 PRINTW$;" (";H!;" STERES)"
1190 PRINT:IFK(E)<32766GOSUB870  
1200 PRINT"GRAIN        GRAIN       PRICE OF   PRICE OF    TREASURY"
1210 PRINT"RESERVE      DEMAND      GRAIN      LAND"
1220 PRINTR(E);TAB(13)D!;TAB(24)G;TAB(36)L;TAB(48)K(E)
1230 PRINT"STERES       STERES      1000 ST.   HECTARES    GOLD FLORINS"
1240 RETURN
1250 J=(J(E)*300-500)*T(E):ONJ(E)GOTO1260 ,1280 ,1300 ,1320 
1260 J$="VERY FAIR"
1270 GOTO1330 
1280 J$="MODERATE"
1290 GOTO1330 
1300 J$="HARSH"
1310 GOTO1330 
1320 J$="OUTRAGEOUS"
1330 Y=150-G(E)-H(E)-I(E):IFY<1Y=1
1340 C!=(N(E)*180+Q(E)*75+M(E)*20)*(Y/100)+U(E)*100
1350 S!=(N(E)*50+M(E)*25+U(E)*10)*(Y/100)*(5-J(E))/2
1360 I!=N(E)*250+U(E)*20+(10*J(E)*N(E))*(Y/100)
1370 C!=C!*G(E)/100:IFC!<32760GOSUB850  
1380 S!=S!*H(E)/100:IFS!<32760GOSUB860  
1390 I!=I!*I(E)/100:IFI!<32760GOSUB840  
1400 PRINT"STATE REVENUES    ";J+C!+S!+I!;"GOLD FLORINS"
1410 PRINT"CUSTOM DUTY","SALES TAX","INCOME TAX","JUSTICE"
1420 PRINTG(E);"%",H(E);"%",I(E);"%",J$
1430 PRINTC!,S!,I!,J;"FL "
1440 RETURN
1450 CLS:PRINT:PRINTT$(E);N$(E)
1460 GOSUB1160 
1470 PRINT
1480 PRINT"1-BUY GRAIN  2-SELL GRAIN  3-BUY LAND  4-SELL LAND"
1490 INPUT"(ENTER 0 TO CONTINUE)";I!
1500 IFI!>4THEN1480 
1510 IFI!<1THENRETURN
1520 ONI!GOTO1530 ,1570 ,1610 ,1640 
1530 INPUT"HOW MUCH GRAIN DO YOU WANT TO BUY";I!
1540 K(E)=K(E)-(I!*G/1000):R(E)=R(E)+I!
1550 CLS:PRINT:PRINTT$(E);N$(E):GOSUB1190 
1560 GOTO1470 
1570 INPUT"HOW MUCH GRAIN DO YOU WISH TO SELL";I!
1580 IFI!>R(E)PRINT"YOU HAVE IT":PRINT:GOTO1570 
1590 K(E)=K(E)+(I!*G/1000):R(E)=R(E)-I!
1600 GOTO1550 
1610 INPUT"HOW MANY HECTARES DO YOU WISH TO BUY";I!
1620 L(E)=L(E)+I!:K(E)=K(E)-(I!*L)
1630 GOTO1550 
1640 INPUT"HOW MANY HECTARES DO YOU WISH TO SELL";I!
1650 IFI!>(L(E)-5000)PRINT"yOU CAN'T SELL THAT MUCH":GOTO1640 
1660 L(E)=L(E)-I!:K(E)=K(E)+(I!*L)
1670 GOTO1550 
1680 CLS:PRINT:PRINTT$(E);N$(E)
1690 PRINT:GOSUB1250 
1700 PRINT:PRINT"1-CUSTOMS DUTY  2-SALES TAX  3-WEALTH TAX  4-JUSTICE"
1710 INPUT"(ENTER TAX NUMBER FOR CHANGES, 0 TO CONTINUE)";I
1720 IFI>4PRINT:GOTO1700 
1730 IFI<1GOTO1880 
1740 ONIGOTO1750 ,1790 ,1820 ,1850 
1750 INPUT"NEW CUSTOMS DUTY (0 TO 100)";I
1760 IFI>100I=100
1770 IFI<0I=0
1780 G(E)=I:GOTO1680 
1790 INPUT"NEW SALES TAX (0 TO 50)";I
1800 IF(I>50)OR(I<0)I=5
1810 H(E)=I:GOTO1680 
1820 INPUT"NEW WEALTH TAX (0 TO 25)";I
1830 IF(I<0)OR(I>25)THENI=0
1840 I(E)=I:GOTO1680 
1850 INPUT"JUSTICE: 1-VERY FAIR   2-MODERATE   3-HARSH   4-OUTRAGEOUS";I
1860 IF(I>4)OR(I<1)THEENI=1
1870 J(E)=I:GOTO1680 
1880 K(E)=K(E)+C!+S!+I!+J
1890 IFK(E)<0THENK(E)=K(E)*1.5
1900 IFK(E)<(-10000*T(E))GOTO3460 
1910 RETURN
1920 PRINT
1930 INPUT"HOW MUCH GRAIN WILL YOU RELEASE FOR CONSUMPTION";G!
1940 IFG!<(R(E)/5)PRINT"YOU MUST RELEASE AT LEAST 20% OF YOUR RESERVES":GOTO1930 
1950 IFG!>(R(E)-(R(E)/5))PRINT"YOU MUST KEEP AT LEAST 20%":GOTO1930 
1960 R(E)=R(E)-G!:CLS:PRINT:PRINTT$(E);N$(E):PRINT
1970 Z=G!/D!-1:IFZ>0Z=Z/2
1980 IFZ>.25Z=Z/10+.25
1990 Z%=50-G(E)-H(E)-I(E):IFZ%<0Z%=Z%*J(E)
2000 Z%=Z%/10:IFZ%>0Z%=Z%+3-J(E)
2010 Z=Z+(Z%/10):IFZ>.5Z=.5
2020 IFG!<(D!-1)THEN2230 
2030 A=7:GOSUB880  
2040 A=3:GOSUB890  
2050 IF(G(E)+H(E))<35M(E)=M(E)+RND(4)
2060 IFI(E)<RND(20)N(E)=N(E)+RND(2)-1:Q(E)=Q(E)+RND(3)-1
2070 IFG!<(D!+D!*.3)THEN2170 
2080 Z%=S(E)/1000
2090 Z=(G!-D!)/D!*10
2100 Z=Z*Z%*RND(25)+RND(40):IFZ>32000Z=32000
2110 Z%=Z:Z=RND(Z%)
2120 PRINTZ;"SERFS MOVE TO THE CITY":S(E)=S(E)+Z
2130 Z%=Z/5:Z=RND(Z%):IFZ>50Z=50
2140 M(E)=M(E)+Z
2150 N(E)=N(E)+1
2160 Q(E)=Q(E)+2
2170 IFJ(E)<3THEN2220 
2180 J!=S(E)/100*(J(E)-2)*(J(E)-2)
2190 J!=RND(J!)
2200 S(E)=S(E)-J!
2210 PRINTJ!;"SERFS FLEE HARSH JUSTICE"
2220 GOTO2280 
2230 X=(D!-G!)/D!*100-9:X%=X:IFX>65X=65
2240 IFX<0X%=0:X=0
2250 A=3:GOSUB880  
2260 A=Z%+8:GOSUB890  
2270 GOTO2170 
2280 Z=A(E)*75:K(E)=K(E)+Z:IFZ>0PRINT"YOUR MARKET EARNED";Z;"FLORINS RENT"
2290 IFS(E)<32766S!=S(E):GOSUB860  :S(E)=S!
2300 Z=D(E)*(55+RND(250)):IFZ>0K(E)=K(E)+Z:PRINT"YOUR WOOLEN MILL EARNED";Z;" FLORINS"
2310 Z=P(E)*3:PRINT"YOU PAID YOUR SOLDIERS";Z;" FLORINS":K(E)=K(E)-Z
2320 IF(L(E)/1000)>P(E)THEN3520 
2330 IF(L(E)/500)<P(E)THEN2370 
2340 FORA=1TOF:IFA=ETHEN2360 
2350 IFP(A)>(P(E)*2.4)THENN8100
2360 NEXT
2370 INPUT"(PRESS ENTER)";A$
2380 RETURN
2390 CLS
2400 L%=(L(E)/1000):IFL%<10X=80:Y=27:GOTO2470 
2410 IFL%<30X=80:Y=27-(L%-10):GOTO2470 
2420 IFL%<50X=60:Y=27-(L%-30):GOTO2470 
2430 IFL%<70X=40:Y=27-(L%-50):GOTO2470 
2440 IFL%<90X=20:Y=27-(L%-70):GOTO2470 
2450 IFL%<110X=1:Y=27-(L%-90):GOTO2470 
2460 X=1:Y=7
2470 FORZ=XTO127:SET(Z,Y):NEXTZ
2480 FORZ=YTO47:SET(X,Z):NEXTZ
2490 IF(P(E)-5)<(L(E)/1000)THEN2530 
2500 FORA=X+1TOX+6:FORB=Y+1TOY+5:SET(A,B):NEXTB:NEXTA:SET(X,Y-1):SET(X+2,Y-1):SET(X+4,Y-1):SET(X+6,Y-1)
2510 IF(P(E)/2)<(L(E)/1000)THEN2530 
2520 FORA=X+7TOX+10:FORB=Y+1TOY+5:SET(A,B):NEXTB:NEXTA:SET(X+8,Y-1):SET(X+10,Y-1):RESET(X+3,Y+2):RESET(X+7,Y+4):SET(X+1,Y-1):SET(X+9,Y-1):SET(X,Y-2):SET(X+2,Y-2):SET(X+8,Y-2):SET(X+10,Y-2)
2530 Z=C(E)+1:IFZ>7THENZ=7
2540 ONZGOTO2620 ,2610 ,2590 ,2580 ,2570 ,2560 ,2550 
2550 FORA=96TO110:SET(A,30):NEXTA:RESET(102,30):RESET(104,30)
2560 FORA=96TO99:FORB=24TO29:SET(A,B):NEXTB:NEXTA
2570 FORA=107TO110:FORB=24TO29:SET(A,B):NEXTB:NEXTA
2580 FORB=22TO24:SET(103,B):NEXTB:SET(102,23):SET(104,23)
2590 FORA=101TO105:FORB=25TO26:SET(A,B):NEXTB:NEXTA
2600 RESET(101,25):RESET(105,25)
2610 FORA=100TO106:FORB=27TO29:SET(A,B):NEXTB:NEXTA:RESET(102,29):RESET(104,29)
2620 Z=B(E)*2:IFZ=0THEN2670 
2630 IFZ>9SET(73,32):SET(75,32):SET(89,32):SET(91,32):SET(74,31):SET(90,31)
2640 IFZ>8Z=9:FORA=80TO84:FORB=30TO32:SET(A,B):NEXTB:NEXTA:RESET(81,32):RESET(83,31):SET(81,29):SET(83,29):SET(82,28)
2650 FORA=(82-Z)TO(82+Z):FORB=33TO35:SET(A,B):NEXTB:NEXTA
2660 FORA=(83-Z)TO(81+Z)STEP2:RESET(A,34):NEXTA:RESET(82,35)
2670 Z=S(E)-D(E)*100:IFZ<1THENZ=1
2680 Z=Z*5/L(E)*10+1:IFZ>10THENZ=10
2690 Z=(Z/10)*(45-Y):Z=INT(47-Z)
2700 FORA=119TO127:SET(A,Z):NEXTA:RESET(122,Z):RESET(123,Z):RESET(125,Z)
2710 FORA=119TO127STEP2:SET(A,Z+1):NEXTA:SET(118,Z-1):SET(127,Z-1)
2720 Z=A(E)*2:IFZ=0THEN2750 
2730 IFZ>((126-X)-2)Z=((126-X)-2)
2740 FORA=XTOX+ZSTEP2:SET(A,39):SET(A+1,39):SET(A+1,40):SET(A+1,41):NEXTA
2750 Z=D(E):IFZ=0THEN2790 
2760 IFZ>(126-X)Z=126-X
2770 FORA=126-ZTO127:FORB=45TO47:SET(A,B):NEXTB:NEXTA
2780 FORA=127-ZTO126STEP2:RESET(A,46):NEXTA
2790 PRINT@644,"YEAR";
2800 PRINT@707,Y(0);
2810 PRINT@0,T$(E);N$(E);"
2820 INPUT"(ENTER 0)";A$
2830 RETURN
2840 CLS:PRINT:PRINTT$(E);N$(E):PRINT"STATE PURCHASES":PRINT
2850 PRINT"1. MARKETPLACE           1000 FLORINS"
2860 PRINT"2. WOOLEN MILL           2000 FLORINS"
2870 PRINT"3. PALACE (PARTIAL)      3000 FLORINS"
2880 PRINT"4. CATHEDRAL (PARTIAL)   5000 FLORINS"
2890 PRINT"5. EQUIP 1 PLATOON ARMY   500 FLORINS"
2900 PRINT:PRINT"YOU HAVE";K(E);"GOLD FLORINS":PRINT
2910 PRINT"TO CONTINUE, ENTER 0, TO COMPARE STANDINGS, 6"
2920 INPUT"YOUR CHOICE";I
2930 CLS:IFI<1RETURN
2940 IFI>5GOSUB620  :GOTO2840 
2950 ONIGOTO3000 ,2960 ,3050 ,3100 ,3150 
2960 D(E)=D(E)+1
2970 K(E)=K(E)-2000
2980 U(E)=U(E)+.25
2990 GOTO2840 
3000 A(E)=A(E)+1
3010 M(E)=M(E)+5
3020 K(E)=K(E)-1000
3030 U(E)=U(E)+.1
3040 GOTO2840 
3050 B(E)=B(E)+1
3060 N(E)=N(E)+RND(2)
3070 K(E)=K(E)-3000
3080 U(E)=U(E)+.5
3090 GOTO2840 
3100 C(E)=C(E)+1
3110 Q(E)=Q(E)+RND(6)
3120 K(E)=K(E)-5000
3130 U(E)=U(E)+1
3140 GOTO2840 
3150 P(E)=P(E)+20
3160 S(E)=S(E)-20
3170 K(E)=K(E)-500
3180 GOTO2840 
3190 Z=0
3200 A=A(E):GOSUB3400 
3210 A=B(E):GOSUB3400 
3220 A=C(E):GOSUB3400 
3230 A=C(E):GOSUB3400 
3240 A=K(E)/5000:GOSUB3400 
3250 A=L(E)/6000:GOSUB3400 
3260 A=M(E)/50:GOSUB3400 
3270 A=N(E)/5:GOSUB3400 
3280 A=P(E)/50:GOSUB3400 
3290 A=Q(E)/10:GOSUB3400 
3300 A=S(E)/2000:GOSUB3400 
3310 A=U(E)/5:GOSUB3400 
3320 A=Z/U(0)-J(E):A=INT(A):IFA>8A=8
3330 IF(Y(0)+2)=O(E)T(E)=T(E)+1
3340 IFT(E)>ATHEN3390 
3350 T(E)=A
3360 RESTORE
3370 FORB=1TO(T(E)+V(E)):READT$(E):NEXT
3380 IFT(E)=8THEN3440 
3390 RETURN
3400 IFA>10:A=10
3410 A=INT(A)
3420 Z=Z+A
3430 RETURN
3440 CLS:PRINT:PRINT"GAME OVER ";T$(E);N$(E);" WINS"
3450 GOSUB2400 :GOSUB620  :GOTO3920 
3460 CLS:PRINT:PRINTT$(E);N$(E);" IS BANKRUPT"
3470 PRINT:PRINT"CREDITORS HAVE SEIZED MUCH OF YOUR ASSETS"
3480 PRINT
3490 INPUT"(PRESS ENTER)";A$
3500 A(E)=0:B(E)=0:C(E)=0:D(E)=0:L(E)=6000:U(E)=1:K(E)=100
3510 RETURN
3520 Z=0:FORA=1TOF
3530 IFA=ETHEN3570 
3540 IFP(A)<P(E)THEN3570 
3550 IFP(A)<(1.2*(L(A)/1000))THEN3570 
3560 IFP(A)>P(Z) Z=A
3570 NEXT
3580 IFZ=0T$(0)=" BARON ":N$(0)="PEPPONE OF MONTERANA":A!=RND(9000)+1000:GOTO3600 
3590 A!=P(Z)*1000-L(Z)/3
3600 IFA!>(L(E)-5000)A!=(L(E)-5000)/2
3610 PRINTT$(Z);N$(Z);" INVADES AND SEIZES";A!
3620 PRINT"HECTARES OF LAND!"
3630 L(Z)=L(Z)+A!:L(E)=L(E)-A!
3640 Z=RND(40):IFZ>(P(E)-15)Z=P(E)-15
3650 PRINTT$(E);N$(E);" LOSES";Z;" SOLDIERS IN BATTLE"
3660 P(E)=P(E)-Z:INPUT"(PRESS ENTER)";A$
3670 RETURN
3680 CLS:PRINT@64,"SANTA PARAVIA AND FIUMACCIO
3690 PRINT@128,"===========================
3700 PRINT@220,"COPYRIGHT 1978 REV. GEORGE BLANK
3710 PRINT@284,"REVISED 8/79 B.YAMASAKI
3720 PRINT@389,"YOU ARE THE RULER OF A 15TH CENTURY ITALIAN CITY-STATE
3730 PRINT@453,"IF YOU RULE WELL, YOU WILL RECEIVE HIGHER TITLES.
3740 PRINT@517,"THE FIRST PLAYER TO BECOME KING OR QUEEN WINS.
3750 PRINT@581,"LIFE EXPECTANCY, THEN WAS RATHER BRIEF.
3760 PRINT@645,"YOU MAY NOT LIVE LONG ENOUGH TO WIN!
3770 PRINT@930,"<<ENTER>> TO TURN PAGE";:INPUTA$
3780 CLS:PRINT"INSTRUCTIONS-CONT'D":PRINT
3790 PRINT"THE COMPUTER WILL DRAW A MAP OF YOUR STATE. THE SIZE
3800 PRINT"OF THE AREA IN THE WALL GROWS AS YOU BUY MORE LAND.":PRINT
3810 PRINT"THE SIZE OF THE GUARD TOWER IN THE UPPER LEFT CORNER SHOWS
3820 PRINT"THE ADEQUACY OF YOUR DEFENCES. IF IT SHRINKS, ARM MORE SOLDIERS!
3830 PRINT"IF THE HORSE AND PLOWMAN IS TOUCHING THE TOP WALL, ALL YOUR
3840 PRINT"LAND IS IN PRODUCTION. OTHERWISE YOU NEED MORE SERFS, WHO WILL
3850 PRINT"MIGRATE TO YOUR STATE IF YOU DISTRIBUTE MORE GRAIN THAN THE 
3860 PRINT"MINIMUM DEMAND. IF YOU DISTRIBUTE LESS GRAIN, SOME OF YOUR PEO-
3870 PRINT"PLE WILL STARVE, AND YOU WILL HAVE A HIGH DEATH RATE.":PRINT
3880 PRINT"HIGH TAXES RAISE MONEY, BUT SLOW DOWN ECONOMIC GROWTH."
3890 PRINT@1000,"<<ENTER>> TO BEGIN";:INPUTA$
3900 CLS:RETURN
3910 GOSUB2390 :GOSUB620  
3920 PRINT"GAME IS OVER: PRESS ENTER FOR NEW GAME"
3930 INPUTA$
3940 GOTO100  
