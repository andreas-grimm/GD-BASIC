10 REM SANTA PARAVIA AND FIUMACCIO VERSION 6.2
20 REM COPYRIGHT 1978 BY REV. GEORGE BLANK  LEECHBURG, PA 
30 REM OCTOBER 16
100 CLEAR 700
150 DEFINT A-J,M-Q,T,V,W
160 Y(0)=1400
200 DATA "SIR ","BARON ","COUNT ","MARQUIS ","DUKE ","GRAND DUKE ","PRINCE ","* H.R.H. KING "
210 DATA "LADY ","BARONESS ","COUNTESS ","MARQUISE ","DUCHESS ","GRAND DUCHESS ","PRINCESS ","* H.R.H. QUEEN "
220 DATA "SANTA PARAVIA","FIUMACCIO","TORRICELLA","MOLINETTO","FONTANILE","ROMAGNA"
299 REM SET UP MATRICES
300 CLS:PRINT:PRINT"SANTA PARAVIA AND FIUMACCIO"
310 PRINT:FOR A= 1 TO 16:READ A$:NEXT
320 INPUT"HOW MANY PEOPLE WANT TO PLAY(1 TO 6)";F
330 FOR A=1 TO F:READ T$(A)
340 PRINT:PRINT"WHO IS THE RULER OF ";T$(A);
350 INPUT N$(A)
360 N$(A)=N$(A)+" OF "+T$(A)
370 PRINT"IS ";N$(A)" A MAN OR A WOMAN";
380 V(A)=0:INPUT A$
390 IF LEFT$(A$,1)="W" V(A)=8
400 G(A)=25:H(A)=10:I(A)=5:J(A)=2:O(A)=1420+RND(35)
410 K(A)=1000:L(A)=10000:R(A)=5000:T(A)=1:U(A)=1
420 N(A)=4:P(A)=25:Q(A)=5:M(A)=25:S(A)=2000
430 NEXT
440 FOR A=1 TO F
450 RESTORE
460 B=V(A)+T(A)
470  FOR C=1 TO B
480  READ T$(A)
490 NEXT C
500 NEXT A
510 INPUT"DO YOU WISH INSTRUCTIONS";A$
520 IF LEFT$(A$,1)="N" THEN 540
530 GOSUB 10000
540 PRINT"1. APPRENTICE 2. JOURNEYMAN 3. MASTER 4. GRAND MASTER"
550 INPUT"ENTER LEVEL OF PLAY DESIRED";U(0)
560 IF U(0)<1 U(0)=1
570 IF U(0)>4 U(0)=4
580 U(0)=U(0)+5
599 REM CONTROL ROUTINE
600 E=E+1
610 IF T(E)=-1 E=E+1
619 REM TEST FOR END OF GAME AFTER DEATH
620 IF(T(1)<1)AND(T(2)<1)AND(T(3)<1)AND(T(4)<1)AND(T(5)<1)AND(T(6)<1) THEN GOTO 11000
630 IF E>F THEN E=0:Y(0)=Y(0)+1:GOTO 600
640 IF Y(0)>O(E) THEN 600
645 IF Y(0)=O(E) THEN 1100
650 GOSUB 2000
660 GOSUB 2600
670 GOSUB 4000
680 GOSUB 3000
690 GOSUB 5000
700 GOSUB 6000
710 GOSUB 7000
990 GOTO 600
999 REM COMPARISON ROUTINE
1000 CLS
1010 PRINT"NOBLES SOLDIERS CLERGY MERCHANTS SERFS   LAND   TREASURY"
1020 PRINT:FOR A=1TOF:PRINT T$(A);N$(A)
1030 PRINT N(A); TAB(7) P(A);TAB(16) Q(A); TAB(23) M(A); TAB(32) S(A); TAB(40) L(A); TAB(50) K(A)
1040 NEXT
1050 PRINT:INPUT"  (PRESS ENTER)";A$
1060 RETURN
1099 REM OBITUARY
1100 CLS:PRINT:PRINT"VERY SAD NEWS":PRINT
1110 PRINT T$(E);N$(E);" HAS JUST DIED"
1120 T(E)=-1:Y=RND(8)
1130 IF Y(0)>1450 PRINT"OF OLD AGE AFTER A LONG REIGN":GOTO 1190
1140 IF Y<4 PRINT"OF PNEUMONIA AFTER A COLD WINTER IN A DRAFTY CASTLE"
1150 IF Y=5 PRINT"IN A SMALLPOX EPIDEMIC"
1160 IF Y=4 PRINT"OF TYPHOID AFTER DRINKING CONTAMINATED WATER"
1170 IF Y=6 PRINT"AFTER BEING ATTACKED BY ROBBERS WHILE TRAVELING"
1180 IF Y>6 PRINT"OF FOOD POISONING"
1190 PRINT
1200 INPUT"(PRESS ENTER)";A$
1210 GOSUB 5000
1220 GOSUB 1000
1290 GOTO 600
1299 REM  MINOR SUBROUTINES
1300 I!=INT(I!):RETURN
1310 C!=INT(C!):RETURN
1320 S!=INT(S!):RETURN
1330 K(E)=INT(K(E)):RETURN
1500 Z=RND(A)*S(E)/100:Z%=Z:PRINT Z%;"SERFS BORN THIS YEAR":S(E)=S(E)+Z%:RETURN
1510 Z=RND(A)*S(E)/100:Z%=Z:PRINT Z%;"SERFS DIE THIS YEAR":S(E)=S(E)-Z%:RETURN
1999 REM AGRICULTURAL CALCULATIONS
2000 W=(RND(5)+RND(6))/2
2010 ON W GOTO 2020,2040,2060,2080,2100
2020 W$="DROUGHT     FAMINE THREATENS"
2030 GOTO 2110
2040 W$="BAD WEATHER    POOR HARVEST"
2050 GOTO 2110
2060 W$="NORMAL WEATHER   AVERAGE HARVEST"
2070 GOTO 2110
2080 W$="GOOD WEATHER   FINE HARVEST"
2090 GOTO 2110
2100 W$="EXCELLENT WEATHER  GREAT HARVEST"
2109 REM RATS EAT GRAIN
2110 R=RND(50)
2120 R(E)=(R(E)*100-R(E)*R)/100
2129 REM GRAIN HARVEST
2130 X=L(E):Y=(S(E)-D(E)*100)*5:IF Y<0 Y=0
2140 IF Y<X THEN X=Y
2150 Y=R(E)*2:IF Y<X THEN X=Y
2160 Y=W-.5:H!=X*Y:R(E)=R(E)+H!
2169 REM GRAIN DEMAND
2170 D!=N(E)*100+C(E)*40+M(E)*30+P(E)*10+S(E)*5
2179 REM PRICE OF LAND
2180 L=(3*W+RND(6)+RND(6)+10)/10
2190 IF H!<1  Y=2:GOTO 2210
2200 Y=D!/H!:IF Y>2 Y=2
2210 IF Y<.8 Y=.8
2220 L=L*Y:L=INT(L*10):L=L/10
2229 REM PRICE OF GRAIN
2230 Z=6-W
2240 G=(Z*3+RND(5)+RND(5))/5*Y*20
2290 RETURN
2299 REM DISPLAY HARVEST (ALTERNATE ENTRY - 2340)
2300 PRINT
2310 PRINT"RATS ATE";R;" % OF YOUR GRAIN RESERVES"
2320 PRINT W$;" (";H!;" STERES)"
2340 PRINT:IF K(E)<32766 GOSUB 1330
2350 PRINT"GRAIN        GRAIN        PRICE OF   PRICE OF   TREASURY"
2360 PRINT"RESERVE      DEMAND       GRAIN      LAND"
2370 PRINT R(E);TAB(13) D!;TAB(25) G;TAB(36) L;TAB(48) K(E)
2380 PRINT"STERES       STERES       1000 ST.   HECTARES    GOLD FLORINS"
2390 RETURN
2399 REM TAXES
2400 J=(J(E)*300-500)*T(E):ON J(E) GOTO 2410,2430,2450,2470
2410 J$="VERY FAIR"
2420 GOTO 2475
2430 J$="MODERATE"
2440 GOTO 2475
2450 J$="HARSH"
2460 GOTO 2475
2470 J$="OUTRAGEOUS"
2475 Y=150-G(E)-H(E)-I(E):IF Y<1 Y=1
2479 REM CALCULATE TAX YIELD
2480 C!=(N(E)*180+Q(E)*75+M(E)+20)*(Y/100)+U(E)*100
2490 S!=(N(E)*50+M(E)*25+U(E)*10)*(Y/100)*(5-J(E))/2
2500 I!=N(E)*250+U(E)*20+(10*J(E)*N(E))*(Y/100)
2510 C!=C!*G(E)/100:IF C!<32760 GOSUB 1310
2520 S!=S!*H(E)/100:IF S!<32760 GOSUB 1320
2530 I!=I!*I(E)/100:IF I!<32760 GOSUB 1300
2540 PRINT"STATE REVENUES    ";J+C!+S!+I!;"GOLD FLORINS"
2542 PRINT"CUSTOMS DUTY","SALES TAX","INCOME TAX","JUSTICE"
2546 PRINT G(E);"%",H(E);"%",I(E);"%",J$
2550 PRINT C!,S!,I!,J;"FL. "
2590 RETURN
2599 REM MANAGE HARVEST
2600 CLS:PRINT:PRINT T$(E);N$(E)
2610 GOSUB 2300
2620 PRINT
2630 PRINT"1. BUY GRAIN  2. SELL GRAIN  3. BUY LAND  4. SELL LAND"
2640 INPUT"(ENTER 0 TO CONTINUE)";I!
2650 IF I!>4 THEN 2630
2660 IF I!<1 THEN RETURN
2670 ON I! GOTO 2700,2750,2800,2850
2700 INPUT"HOW MUCH GRAIN DO YOU WANT TO BUY";I!
2710 K(E)=K(E)-(I!*G/1000):R(E)=R(E)+I!
2720 CLS:PRINT:PRINT T$(E);N$(E):GOSUB 2340
2730 GOTO 2620
2750 INPUT"HOW MUCH GRAIN DO YOU WISH TO SELL";I!
2760 IF I!>R(E) PRINT"YOU DONT HAVE IT":PRINT:GOTO 2750
2770 K(E)=K(E)+(I!*G/1000):R(E)=R(E)-I!
2780 GOTO 2720
2800 INPUT"HOW MANY HECTARES DO YOU WANT TO BUY";I!
2810 L(E)=L(E)+I!:K(E)=K(E)-(I!*L)
2820 GOTO 2720
2850 INPUT"HOW MANY HECTARES DO YOU WISH TO SELL";I!
2860 IF I!>(L(E)-5000) PRINT"YOU CANT SELL THAT MUCH":GOTO 2850
2870 L(E)=L(E)-I!:K(E)=K(E)+(I!*L)
2880 GOTO 2720
2999 REM DISPLAY TAXES
3000 CLS:PRINT:PRINT T$(E);N$(E)
3010 PRINT:GOSUB 2400
3020 PRINT:PRINT"1.  CUSTOMS  2.  SALES TAX  3.  WEALTH TAX  4.  JUSTICE"
3030 INPUT"(ENTER TAX NUMBER FOR CHANGES, 0 TO CONTINUE)";I
3040 IF I>4 PRINT:GOTO3020
3050 IF I<1 GOTO 3200
3060 ON I GOTO 3070,3110,3140,3170
3070 INPUT"ENTER NEW CUSTOMS DUTY (0 TO 100)";I
3080 IF I>100 I=100
3090 IF IV0 IU0
3100 G(E)=I:GOTO 3000
3110 INPUT"NEW SALES TAX (0 TO 25)";I
3120 IF(I>50) OR(I<0) I=5
3130 H(E)=I:GOTO 3000
3140 INPUT"NEW WEALTH TAX (0 TO 25)";I
3150 IF(I<0) OR (I>25) THEN I=0
3160 I(E)=I:GOTO 3000
3170 INPUT"JUSTICE: 1. VERY FAIR  2. MODERATE  3. HARSH  4. OUTRAGEOUS";I
3180 IF(I>4) OR (I<1) THEN I=1
3190 J(E)=I:GOTO 3000
3200 K(E)=K(E)+C!+S!+I!+J
3209 REM INTEREST CHARGE
3210 IF K(E)<0 THEN K(E)=K(E)+1.5
3219 REM BANKRUPTCY TEST
3220 IF K(E)<(-10000*T(E)) GOTO 8000
3990 RETURN
3999 REM POPULATION
4000 PRINT
4010 INPUT"HOW MUCH GRAIN WILL YOU RELEASE FOR CONSUMPTION";G!
4020 IF G!<(R(E)/5) PRINT"YOU MUST RELEASE AT LEAST 20% OF YOUR RESERVES":GOTO 4010
4030 IF G!>(R(E)-(R(E)/5)) PRINT"YOU MUST KEEP AT LEAST 20%":GOTO 4010
4035 R(E)=R(E)-G!:CLS:PRINT:PRINT T$(E);N$(E):PRINT
4040 Z=G!/D!-1:IF Z>0 Z=Z/2
4050 IF Z>.25 Z=Z/10+.25
4060 Z%=50-G(E)-H(E)-I(E):IF Z%<0 Z%=Z%*J(E)
4070 Z%=Z%/10:IF Z%>0 Z%=Z%+3-J(E)
4080 Z=Z+(Z%/10):IF Z>.5 Z=.5
4100 IF G!<(D!-1) THEN 4500
4110 A=7:GOSUB 1500
4120 A=3:GOSUB 1510
4130 IF (G(E)+H(E))<35 M(E)=M(E)+RND(4)
4140 IF I(E)<RND(20) N(E)=N(E)+RND(2)-1:Q(E)=Q(E)+RND(3)-1
4190 IF G!<(D!+D!*.3) THEN 4300
4200 Z%= S(E)/1000
4210 Z=(G!-D!)/D!*10
4220 Z=Z*Z%*RND(25)+RND(40):IF Z>32000 Z=32000
4230 Z%=Z:Z=RND(Z%)
4240 PRINT Z;"SERFS MOVE TO THE CITY":S(E)=S(E)+Z
4250 Z%=Z/5:Z=RND(Z%):IF Z>50 Z=50
4260 M(E)=M(E)+Z
4270 N(E)=N(E)+1
4280 Q(E)=Q(E)+2
4299 REM INJUSTICE PENALTY
4300 IF J(E)<3 THEN 4490
4310 J!=S(E)/100*(J(E)-2)*(J(E)-2)
4320 J!=RND(J!)
4330 S(E)=S(E)-J!
4340 PRINT J!;"SERFS FLEE HARSH JUSTICE"
4490 GOTO 4900
4499 REM FOOD SHORTAGE
4500 X=(D!-G!)/D!*100-9:X%=X:IF X>65 X=65
4505 IF X<0 X%=0:X=0
4510 A=3:GOSUB 1500
4520 A=X%+8:GOSUB 1510
4530 GOTO 4300
4900 Z=A(E)*75:K(E)=K(E)+Z:IF Z>0 PRINT"YOUR MARKET EARNED";Z;"FLORINS RENT"
4910 IF S(E)<32766 S!=S(E):GOSUB 1320:S(E)=S!
4920 Z=D(E)*(55+RND(250)):IF Z>0 K(E)=K(E)+Z:PRINT"YOUR WOOLEN MILL EARNED";Z;"FLORINS"
4930 Z=P(E)*3:PRINT"YOU PAID YOUR SOLDIERS";Z;" FLORINS":K(E)=K(E)-Z
4939 REM INVASION
4940 IF (L(E)/1000)>P(E) THEN 8100
4945 IF (L(E)/500)<P(E) THEN 4980
4950 FOR A=1 TO F:IF A=E THEN 4970
4960 IF P(A)>P(E)*2.4 THEN 8100
4970 NEXT
4980 INPUT"(PRESS ENTER)";A$
4990 RETURN
4999 REM DRAW MAP
5000 CLS
5010 L%=(L(E)/1000):IF L%<10 X=80:Y=27:GOTO 5100
5020 IF L%<30 X=80:Y=27-(L%-10):GOTO 5100
5030 IF L%<50 X=60:Y=27-(L%-30):GOTO 5100
5040 IF L%<70 X=40:Y=27-(L%-50):GOTO 5100
5050 IF L%<90 X=20:Y=27-(L%-70):GOTO 5100
5060 IF L%<110 X=1:Y=27-(L%-90):GOTO 5100
5070 X=1:Y=7
5100 FOR Z=XTO127:SET(Z,Y):NEXT Z
5110 FOR Z=YTO47:SET(X,Z):NEXT Z
5120 IF P(E)-5<(L(E)/1000) THEN 5160
5130 FOR A=X+1TOX+6:FOR B=Y+1TOY+5:SET(A,B):NEXT B:NEXT A:SET(X,Y-1):SET(X+2,Y-1):SET(X+4,Y-1):SET(X+6,Y-1)

5140 IF (P(E)/2)<(L(E)/1000) THEN 5160
5150 FOR A=X+7TOX+10:FOR B=Y+1TOY+5:SET(A,B):NEXT B:NEXT A:SET(X+8,Y-1):SET(X+10,Y-1):RESET(X+3,Y+2):RESET(X+7,Y+4):SET(X+1,Y-1):SET(X+9,Y-1):SET(X,Y-2):SET(X+2,Y-2):SET(X+8,Y-2):SET(X+10,Y-2)
5160 Z=C(E)+1:IF Z>7 THE Z=7
5170 ON Z GOTO 5250,5240,5230,5220,5210,5200,5180
5180 FOR A=96TO110:SET(A,30):NEXT A:RESET(102,30):RESET(104,30)
5200 FOR A=96TO99:FOR B=24TO29:SET(A,B):NEXT B:NEXT A
5210 FOR A=107TO110:FORB=24TO29:SET(A,B):NEXT B:NEXT A
5220 FOR B=22TO24:SET(103,B):NEXT B:SET(102,23):SET(104,23)
5230 FOR A=101TO105:FOR B=25TO26:SET(A,B):NEXT B:NEXT A
5235  RESET(101,25):RESET(105,25)
5240 FOR A=100TO106:FOR B=27TO29:SET(A,B):NEXT B:NEXT A:RESET(102,29):RESET(104,29)
5250 Z=B(E)*2:IFZ=0 THEN 5300
5255 IF Z>9 SET(73,32):SET(75,32):SET(89,32):SET(91,32):SET(74,31):SET(90,31)
5260 IF Z>8 Z=9:FOR A=80TO84:FOR B=30TO32:SET(A,B):NEXT B:NEXT  A:RESET(81,32):RESET(83,31):SET(81,29):SET(83,29):SET(82,28)
5270 FOR A=(82-Z)TO(82+Z):FOR B=33TO35:SET(A,B):NEXT B:NEXT A
5280 FOR A=(83-Z)TO(81+Z) STEP 2:RESET(A,34):NEXT A:RESET(82,35)
5300 Z=S(E)-D(E)*100:IF Z<1 THEN Z=1
5305 Z=Z*5/L(E)*10+1:IF Z>10 THEN Z=10
5310 Z=(Z/10)*(45-Y):Z=INT(47-Y)
5320 FOR A=119TO127:SET(A,Z):NEXT A:RESET(122,Z):RESET(123,Z):RESET(125,Z)
5330 FOR A=119TO127 STEP 2:SET(A,Z+1):NEXT A:SET(118,Z-1):SET(127,Z-1)
5400 Z=A(E)*2:IF Z=0 THEN 5500
5410 IF Z>((126-X)-2) Z=((126-X)-2)
5420 FOR A=XTOX+Z STEP 2:SET(A,39):SET(A+1,39):SET(A+1,40):SET(A+1,41):NEXT A
5500 Z=D(E):IF Z=0 THEN 5900
5510 IF Z>(126-X) Z=126-X
5520 FOR A=126-ZTO127:FOR B=45TO47:SET(A,B):NEXT B:NEXT A
5530 FOR A=127-ZTO126 STEP 2:RESET(A,46):NEXT A
5900 PRINT@ 644,"YEAR";
5920 PRINT@ 707,Y(0);
5930 PRINT@ 0,T$(E);N$(E);"
5940 INPUT"(ENTER 0)";A$
5990 RETURN
5999 REM INVESTMENTS
6000 CLS:PRINT:PRINT T$(E);N$(E):PRINT"STATE PURCHASES":PRINT
6010 PRINT"1.  MARKET PLACE               1000 FLORINS"
6020 PRINT"2.  WOOLEN MILL                2000 FLORINS"
6030 PRINT"3.  PALACE(PARTIAL)            3000 FLORINS"
6040 PRINT"4.  CATHEDRAL(PARTIAL)         5000 FLORINS"
6050 PRINT"5.  EQUIP ONE PLATOON OF SERFS AS SOLDIERS   500 FLORINS"
6060 PRINT:PRINT"YOU HAVE";K(E);"GOLD FLORINS":PRINT
6070 PRINT"TO CONTINUE, ENTER 0, TO COMPARE STANDINGS ENTER 6"
6080 INPUT"YOUR CHOICE";I
6090 CLS:IF I<1 RETURN
6100 IF I>5 GOSUB 1000:GOTO 6000
6110 ON I GOTO 6200,6120,6300,6400,6500
6120 D(E)=D(E)+1
6130 K(E)=K(E)-2000
6140 U(E)=U(E)+.25
6150 GOTO 6000
6200 A(E)=A(E)+1
6210 M(E)=M(E)+5
6220 K(E)=K(E)-1000
6230 U(E)=U(E)+.1
6250 GOTO 6000
6300 B(E)=B(E)+1
6310 N(E)=N(E)+RND(2)
6320 K(E)=K(E)-3000
6330 U(E)=U(E)+.5
6350 GOTO 6000
6400 C(E)=C(E)+1
6410 Q(E)=Q(E)+RND(6)
6420 K(E)=K(E)-5000
6430 U(E)=U(E)+1
6450 GOTO 6000
6500 P(E)=P(E)+20
6510 S(E)=S(E)-20
6520 K(E)=K(E)-500
6550 GOTO 6000
6999 REM CALCULATE NEW TITLE
7000 Z=0
7010 A=A(E):GOSUB 7500
7020 A=B(E):GOSUB 7500
7030 A=C(E):GOSUB 7500
7040 A=D(E):GOSUB 7500
7050 A=K(E)/5000:GOSUB 7500
7060 A=L(E)/6000:GOSUB 7500
7070 A=M(E)/50:GOSUB 7500
7080 A=N(E)/5:GOSUB 7500
7090 A=P(E)/50:GOSUB 7500
7100 A=Q(E)/10:GOSUB 7500
7110 A=S(E)/2000:GOSUB 7500
7120 A=U(E)/5:GOSUB 7500
7130 A=Z/U(0)-J(E):A=INT(A):IF A>8 A=8
7140 IF (Y(0)+2)=O(E) T(E)=T(E)+1
7150 IF T(E)>A THEN 7490
7160 T(E)=A
7170 RESTORE
7180 FOR B=1 TO (T(E)+V(E)):READ T$(E):NEXT
7190 IF T(E)=8 THEN 7600
7490 RETURN
7500 IF A>10 A=10
7520 A=INT(A)
7530 Z=Z+A
7540 RETURN
7599 REM KING OR QUEEN
7600 CLS:PRINT:PRINT"GAME OVER ";T$(E);N$(E);" WINS"
7610 GOSUB 5010:GOSUB 1000:GOTO 11010
7999 REM BANKRUPTCY
8000 CLS:PRINT:PRINT T$(E);N$(E);" IS BANKRUPT"
8010 PRINT:PRINT"CREDITORS HAVE SEIZED MUCH OF YOUR ASSETS"
8020 PRINT
8030 INPUT"(PRESS ENTER)";A$
8040 A(E)=0:B(E)=0:C(E)=0:D(E)=0:L(E)=6000:U(E)=1:K(E)=100
8050 RETURN
8099 REM INVASION (FROM 4940)
8100 Z=0:FOR A=1TOF
8110 IF A=E THEN 8200
8120 IF P(A)<P(E) THEN 8200
8130 IF P(A)<(1.2*(L(A)/1000)) THEN 8200
8140 IF P(A)>P(Z) Z=A
8200 NEXT
8205 IF Z=0 T$(0)=" BARON ":N$(0)="PEPPONE OF MONTERANA":A!=RND(9000)+1000:GOTO 8220
8210 A!=P(Z)*1000-L(Z)/3
8220 IF A!>(L(E)-5000) A!=(L(E)-5000)/2
8230 PRINT T$(Z);N$(Z);" INVADES AND SEIZES";A!
8240 PRINT"HECTARES OF LAND!"
8250 L(Z)=L(Z)+A!:L(E)=L(E)-A!
8260 Z=RND(40):IF Z>(P(E)-15) Z=P(E)-15
8270 PRINT T$(E);N$(E);" LOSES";Z;" SOLDIERS IN BATTLE"
8280 P(E)=P(E)-Z:INPUT"(PRESS ENTER)";A$
8290 RETURN
9999 REM INSTRUCTIONS
10000 CLS:PRINT"SANTA PARAVIA AND FIUMACCIO"
10010 PRINT"   YOU ARE THE RULER OF A 15TH CENTURY ITALIAN CITY-STATE. "
10020 PRINT"IF YOU RULE WELL, YOU WILL RECEIVE HIGHER TITLES. THE"
10030 PRINT"FIRST PLAYER TO BECOME A KING OR QUEEN WINS. LIFE EXPECTANCY"
10040 PRINT"THEN WAS BREIF, SO YOU MAY NOT LIVE LONG ENOUGH TO WIN."
10050 PRINT"   THE COMPUTER WILL DRAW A MAP OF YOUR STATE. THE SIZE"
10060 PRINT"OF THE AREA IN THE WALL GROWS AS YOU BUY MORE LAND. THE"
10070 PRINT"SIZE OF THE GUARD TOWER IN THE UPPER LEFT CORNER SHOWS"
10080 PRINT"THE ADEQUACY OF YOUR DEFENSES. IF IT SHRINKS, EQUIP MORE"
10090 PRINT"SOLDIERS! IF THE HORSE AND PLOWMAN IS TOUCHING THE TOP WALL,"
10100 PRINT"ALL YOUR LAND IS IN PRODUCTION. OTHERWISE YOU NEED MORE"
10110 PRINT"SERFS, WHO WILL MIGRATE TO YOUR STATE IF YOU DISTRIBUTE"
10120 PRINT"MORE GRAIN THAN THE MINIMUM DEMAND. IF YOU DISTRIBUTE LESS"
10130 PRINT"GRAIN, SOME OF YOUR PEOPLE WILL STARVE, AND YOU WILL HAVE"
10140 PRINT"A HIGH DEATH RATE. HIGH TAXES RAISE MONEY, BUT SLOW DOWN"
10150 PRINT"ECONOMIC GROWTH.     (PRESS ENTER TO BEGIN GAME)";
10160 INPUT A$
10170 CLS:RETURN
10999 REM END GAME
11000 GOSUB 5000:GOSUB 1000
11010 PRINT" GAME IS OVER: PRESS ENTER FOR NEW GAME"
11020 INPUT A$
11030 GOTO 10
